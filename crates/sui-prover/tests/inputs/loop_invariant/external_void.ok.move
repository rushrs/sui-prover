module 0x42::loop_invariant_external_void_ok;

use prover::prover::{requires, ensures, clone};

#[ext(spec_only(loop_inv(target = test0_spec)), no_abort)] #[allow(unused_function)]
fun loop_inv_0(i: u64, n: u64) {
    ensures(i <= n);
}

#[ext(spec_only(loop_inv(target = test1_spec)), no_abort)] #[allow(unused_function)]
fun loop_inv_1(i: u64, n: u64, s: u128) {
    ensures(i <= n);
    ensures(s == (i as u128) * ((i as u128) + 1) / 2);
}

#[ext(spec_only(loop_inv(target = test2_spec)), no_abort)] #[allow(unused_function)]
fun loop_inv_2(n: u64, old_n: u64, s: u128) {
    ensures(n <= old_n);
    ensures(s == ((old_n as u128) - (n as u128)) * ((old_n as u128) + (n as u128) + 1) / 2);
}

#[ext(spec_only(loop_inv(target = test3_spec)), no_abort)] #[allow(unused_function)]
fun loop_inv_3(i: u64, n: u64, s: u128) {
    ensures(i < n);
    ensures(s == (i as u128) * ((i as u128) + 1) / 2);
}

#[ext(spec_only(loop_inv(target = test4_spec)), no_abort)] #[allow(unused_function)]
fun loop_inv_4(i: u64, n: u64, p: &u128) {
    ensures(i <= n);
    ensures(*p == (i as u128) * ((i as u128) + 1) / 2);
}

#[ext(spec(prove))] #[allow(unused_function)]
fun test0_spec(n: u64) {
    let mut i = 0;

    while (i < n) {
        i = i + 1;
    };

    ensures(i == n);
}

#[ext(spec(prove))] #[allow(unused_function)]
fun test1_spec(n: u64): u128 {
    let mut s: u128 = 0;
    let mut i = 0;

    while (i < n) {
        i = i + 1;
        s = s + (i as u128);
    };

    ensures(s == (n as u128) * ((n as u128) + 1) / 2);
    s
}

#[ext(spec(prove))] #[allow(unused_function)]
fun test2_spec(mut n: u64): u128 {
    let mut s: u128 = 0;

    let old_n: &u64 = clone!(&n);
    while (n > 0) {
        s = s + (n as u128);
        n = n - 1;
    };

    ensures(s == (*old_n as u128) * ((*old_n as u128) + 1) / 2);
    s
}

#[ext(spec(prove))] #[allow(unused_function)]
fun test3_spec(n: u64): u128 {
    requires(0 < n);

    let mut s: u128 = 0;
    let mut i = 0;

    loop {
        i = i + 1;
        s = s + (i as u128);
        if (i >= n) {
            break
        }
    };

    ensures(s == (n as u128) * ((n as u128) + 1) / 2);
    s
}

#[ext(spec(prove))] #[allow(unused_function)]
fun test4_spec(n: u64): u128 {
    let mut s: u128 = 0;
    let mut i = 0;
    let p: &mut u128 = &mut s;

    while (i < n) {
        i = i + 1;
        *p = *p + (i as u128);
    };

    ensures(s == (n as u128) * ((n as u128) + 1) / 2);
    s
}
